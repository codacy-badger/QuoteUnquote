plugins {
    id 'com.android.library'
}
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 29
    buildToolsVersion '30.0.2'

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    lintOptions {
        abortOnError false
        warningsAsErrors false
        checkAllWarnings = true
        xmlReport true
        htmlReport true
    }

    buildTypes {
        def appcenterKeyProd = System.getenv("APPCENTER_KEY_PROD_QUOTEUNQUOTE")
        if (appcenterKeyProd == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyProd = properties.getProperty('APPCENTER_KEY_PROD_QUOTEUNQUOTE')
        }

        def appcenterKeyDev = System.getenv("APPCENTER_KEY_DEV_QUOTEUNQUOTE")
        if (appcenterKeyDev == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyDev = properties.getProperty('APPCENTER_KEY_DEV_QUOTEUNQUOTE')
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
        }
        debug {
            testCoverageEnabled true

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyDev\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'version'
    productFlavors {
        'googleplay' {
            dimension 'version'
        }
        'fdroid' {
            dimension 'version'
        }
        'integrationTest' {
            dimension 'version'
        }
    }

    sourceSets {
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        integrationTest {
            java.srcDirs = googleplay.java.srcDirs
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'com.github.guness:robolectricsqlite:1.0.8'

    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.microsoft.appcenter:appcenter-analytics:3.2.2"
    implementation "com.microsoft.appcenter:appcenter-crashes:3.2.2"
    implementation "org.robolectric:robolectric:4.4"

    implementation 'androidx.test.ext:junit:1.1.2'


    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation 'junit:junit:4.13.1'
}

repositories {
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/jameshnsears/RobolectricSQLite")
        credentials {
            // from local.properties, then ~/.bashrc
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            username = properties.getProperty("GITHUB_USERNAME") ?: System.getenv("GITHUB_USERNAME")
            password = properties.getProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}