apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
//apply plugin: 'com.github.triplet.play'

apply from: '../checkstyle.gradle'
apply from: '../coveralls.gradle'
apply from: '../jacoco.gradle'
apply from: '../sonarqube.gradle'

apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'

task espresso(type: Exec) {
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'window_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '0'
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.github.jameshnsears.quoteunquote"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 35
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude "**/module-info.class"
        exclude 'LICENSE'
        exclude 'README.md'
    }

    lintOptions {
        abortOnError false
        warningsAsErrors false
        checkAllWarnings = true
        xmlReport true
        htmlReport true
        disable "UnsafeExperimentalUsageError", "UnsafeExperimentalUsageWarning"
    }

    buildTypes {
        def useProdDb = false

        def appcenterKeyProd = System.getenv("APPCENTER_KEY_PROD_QUOTEUNQUOTE")
        if (appcenterKeyProd == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyProd = properties.getProperty('APPCENTER_KEY_PROD_QUOTEUNQUOTE')
        }

        def appcenterKeyDev = System.getenv("APPCENTER_KEY_DEV_QUOTEUNQUOTE")
        if (appcenterKeyDev == null) {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            appcenterKeyDev = properties.getProperty('APPCENTER_KEY_DEV_QUOTEUNQUOTE')
        }

        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short=8', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
            buildConfigField("boolean", "USE_PROD_DB", "$useProdDb")
            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", '"https://us-central1-qualified-glow-281314.cloudfunctions.net/favourites"')
        }
        debug {
            testCoverageEnabled true

            buildConfigField("String", "APPCENTER_KEY", "\"$appcenterKeyProd\"")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
            buildConfigField("boolean", "USE_PROD_DB", "$useProdDb")
            buildConfigField("String", "REMOTE_DEVICE_ENDPOINT", '"https://us-central1-virtual-ego-281313.cloudfunctions.net/favourites"')
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding = true
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true

            returnDefaultValues = true
            animationsDisabled = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }
    flavorDimensions 'version'
    productFlavors {
        'googleplay' {
            dimension 'version'
            applicationIdSuffix ".googleplay"
            versionNameSuffix "-googleplay"
            //             buildConfigField "String", "HOSTNAME", '"dummy"'

        }
        'fdroid' {
            dimension 'version'
            applicationIdSuffix ".fdroid"
            versionNameSuffix "-fdroid"

        }
    }

    sourceSets {
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
//            manifest.srcFile "src/fdroid/AndroidManifest.xml"
        }
    }
    buildToolsVersion '30.0.2'
}

dependencies {
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.3.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation "androidx.work:work-testing:2.4.0"
    androidTestImplementation 'com.google.truth:truth:1.1'
    androidTestImplementation 'org.mockito:mockito-android:3.6.0'
    androidTestImplementation 'org.mockito:mockito-core:3.6.0'
    annotationProcessor "androidx.room:room-compiler:2.2.5"
    debugImplementation "androidx.fragment:fragment-testing:1.2.5"
    debugImplementation 'androidx.test:core:1.3.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.room:room-guava:2.2.5"
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-rxjava2:2.2.5"
    implementation "androidx.work:work-runtime:2.4.0"
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'commons-codec:commons-codec:20041127.091804'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
    implementation project(path: ':utilsLibrary')
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'androidx.test:runner:1.3.0'
    testImplementation "io.mockk:mockk:1.10.2"
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.mockito:mockito-core:3.6.0'
    testImplementation "org.robolectric:robolectric:4.4"
    testImplementation 'org.slf4j:slf4j-api:1.7.30'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.30'

}
repositories {
    mavenCentral()
}
