apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

//apply plugin: 'com.github.triplet.play'

apply from: '../ktlint.gradle'
apply from: '../detekt.gradle'
apply from: '../checkstyle.gradle'

task espresso(type: Exec) {
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'window_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '0'
    commandLine 'adb', 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '0'
}

android {
    compileSdkVersion 29
    buildToolsVersion '30.0.2'

    defaultConfig {
        applicationId "com.github.jameshnsears.quoteunquote"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 35
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude "**/module-info.class"
        exclude 'LICENSE'
        exclude 'README.md'
    }

    lintOptions {
        abortOnError true
        warningsAsErrors false
        checkAllWarnings = true
        xmlReport false
        htmlReport true
    }

    buildTypes {
        def useProdDb = false

        def gitHash = { ->
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-parse', '--short=8', 'HEAD'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("boolean", "USE_PROD_DB", "$useProdDb")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
        }
        debug {
            testCoverageEnabled true
            buildConfigField("boolean", "USE_PROD_DB", "$useProdDb")
            buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions 'version'
    productFlavors {
        'googleplay' {
            dimension 'version'
            applicationIdSuffix ".googleplay"
            versionNameSuffix "-googleplay"
        }
        'fdroid' {
            dimension 'version'
            applicationIdSuffix ".fdroid"
            versionNameSuffix "-fdroid"
        }
        'manifestTest' {
            dimension 'version'
        }
    }

    sourceSets {
        androidTest {
            assets.srcDirs = ['src/main/assets', 'src/androidTest/assets/', 'src/debug/assets/']
            java.srcDirs = ['src/main/java', 'src/androidTest/java', 'src/debug/java']
        }
        googleplay {
            java.srcDirs = ['src/main/java', 'src/googleplay/java']
        }
        fdroid {
            java.srcDirs = ['src/main/java', 'src/fdroid/java']
        }
        manifestTest {
            java.srcDirs = ['src/main/java', 'src/manifestTest/java']
        }
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true

            returnDefaultValues = true
            animationsDisabled = true
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.room:room-testing:2.2.6"
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'

    annotationProcessor 'androidx.room:room-compiler:2.2.6'

    debugImplementation "androidx.fragment:fragment-testing:1.2.5"
    debugImplementation 'androidx.test:core:1.3.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'

    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.room:room-guava:2.2.6'
    implementation 'androidx.room:room-runtime:2.2.6'
    implementation 'androidx.room:room-rxjava2:2.2.6'
    implementation "androidx.work:work-runtime:2.4.0"
    implementation 'com.google.android.material:material:1.2.1'
    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation project(path: ':cloudLib')
    implementation project(path: ':utilsLib')

    manifestTestImplementation "androidx.arch.core:core-testing:2.1.0"
    manifestTestImplementation 'androidx.test:core-ktx:1.3.0'
    manifestTestImplementation 'androidx.test.ext:junit:1.1.2'
    manifestTestImplementation 'io.mockk:mockk:1.10.4'
    manifestTestImplementation 'junit:junit:4.13.1'
    manifestTestImplementation "org.robolectric:robolectric:4.4"

    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation 'androidx.test:core-ktx:1.3.0'
    testImplementation 'androidx.test.ext:junit:1.1.2'
    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.robolectric:robolectric:4.4"
}

repositories {
    mavenCentral()
}
